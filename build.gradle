group 'vtp'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

def buildTags = '~@test'
if (project.hasProperty("tags")) {
    buildTags = tags + ',~@wip'
}
task cucumber {
    dependsOn assemble, compileTestJava

    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output

            systemProperties System.getProperties()

            args = ['--plugin', 'pretty',
                    '--plugin', 'junit:build/reports/junit.xml',
                    '--plugin', 'html:build/reports/cucumber',
                    '--plugin', 'json:build/reports/cucumber.json',
                    '--glue', 'stepdefs',
                    'src/test/resources']

            println "Using tags $buildTags"
            buildTags.split(',').each { tag ->
                args '--tags', "$tag"

            }
        }
    }
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '+'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.53.0'
    compile group: 'info.cukes', name: 'cucumber-core', version: '1.2.4'
    compile group: 'info.cukes', name: 'cucumber-java', version: '1.2.4'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}
